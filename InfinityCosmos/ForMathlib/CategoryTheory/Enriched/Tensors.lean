/-
Copyright (c) ? All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: ? and Arnoud van der Leer
-/

import InfinityCosmos.ForMathlib.CategoryTheory.Enriched.MonoidalProdCat
import Mathlib.CategoryTheory.Enriched.Opposite
import Mathlib.CategoryTheory.Closed.Enrichment

/-!
  # Tensors in an enriched category

  Let `C` be a `V`-enriched category.

  This file defines a pretensor of `v : V` and `c : C` as an object `obj : C`, together with a
  morphism `cone : v ‚ü∂ (x ‚ü∂[V] obj)`.
  Based on this, it defines a family of morphisms `coneNatTrans : (obj ‚ü∂[V] y) ‚ü∂ (x ‚ü∂[V] y)^v`.

  It defines a tensor "`v ‚äó c`" as a pretensor where the `coneNatTrans` morphisms are isomorphisms.
  Subsequently, it constructs the left and right whiskering of morphisms.

  Most of this file is devoted to showing that tensoring, together with whiskering, constitutes
  a V-enriched functor `tensor_bifunc : V ‚äó C ‚ü∂ C`.
-/

universe u u‚ÇÅ v w

open CategoryTheory MonoidalCategory MonoidalClosed BraidedCategory SymmetricCategory

open scoped MonoidalClosed

namespace CategoryTheory

namespace Enriched


-- Copied "notation" from Cotensors
abbrev Ihom (V : Type u) [Category.{u‚ÇÅ, u} V] [MonoidalCategory V] [MonoidalClosed V] (x y : V) : V :=
  (ihom x).obj y

@[nolint unusedArguments]
abbrev Ehom (V : Type u) [Category.{u‚ÇÅ, u} V] [MonoidalCategory V] [MonoidalClosed V]
    (C : Type v) [EnrichedCategory V C] (x y : C) : V :=
  @EnrichedCategory.Hom V _ _ _ _ x y

-- New stuff
variable {V : Type u} [Category.{u‚ÇÅ, u} V] [MonoidalCategory V]
  [MonoidalClosed V]
variable {C : Type v} [EnrichedCategory V C]

/-- A pretensor is an object `obj : C`, together with a morphism `cone : v ‚ü∂ (x ‚ü∂[V] obj)`. -/
structure Pretensor (v : V) (x : C) where
  obj : C
  cone : v ‚ü∂ (Ehom V C x obj)

/-- The family of morphisms from `vx ‚ü∂[V] y` to `(x ‚ü∂[V] y)^v` -/
def Pretensor.coneNatTrans {v : V} {x : C} (vx : Pretensor v x) (y : C) :
  (Ehom V C vx.obj y) ‚ü∂ (Ihom V v (Ehom V C x y)) :=
  curry (vx.cone ‚ñ∑ Ehom V C vx.obj y ‚â´ eComp V x vx.obj y)

/-- Since `Pretensor.coneNatTrans` is defined by currying, its uncurrying can be simplified. -/
lemma Pretensor.uncurry_coneNatTrans {v : V} {x : C} (vx : Pretensor v x) (y : C)
  : uncurry (vx.coneNatTrans y) = vx.cone ‚ñ∑ _ ‚â´ eComp V x vx.obj y :=
  uncurry_curry _

/-- A `Tensor` is a `Pretensor` such that `coneNatTransInv` is an inverse to `coneNatTrans`. -/
structure Tensor (v : V) (x : C) extends (Pretensor v x) where
  coneNatTransInv (y : C) :
    (Ihom V v (Ehom V C x y)) ‚ü∂ (Ehom V C obj y)
  NatTransInv_NatTrans_eq (y : C) :
    (coneNatTransInv y ‚â´ Pretensor.coneNatTrans toPretensor y = ùüô _)
  NatTrans_NatTransInv_eq (y : C) :
    (Pretensor.coneNatTrans toPretensor y ‚â´ coneNatTransInv y = ùüô _)

/-- For a tensor, the morphisms `Pretensor.coneNatTrans` are isomorphisms, with inverses given by
  `Tensor.coneNatTransInv`. -/
instance {v : V} {x : C} {vx : Tensor v x} {y : C} : IsIso (vx.coneNatTrans y) where
  out := ‚ü®vx.coneNatTransInv y, {
    left := vx.NatTrans_NatTransInv_eq y
    right := vx.NatTransInv_NatTrans_eq y
  }‚ü©

/-- For a tensor, the morphisms `Tensor.coneNatTransInv` are isomorphisms, with inverses given by
  `Pretensor.coneNatTrans`. -/
instance {v : V} {x : C} {vx : Tensor v x} {y : C} : IsIso (vx.coneNatTransInv y) where
  out := ‚ü®vx.coneNatTrans y, {
    left := vx.NatTransInv_NatTrans_eq y
    right := vx.NatTrans_NatTransInv_eq y
  }‚ü©

namespace Tensor

variable (V : Type u) [Category.{u‚ÇÅ} V] [MonoidalCategory V] [MonoidalClosed V]
variable {C : Type v} [EnrichedCategory V C]

/-- Whiskering on the right by `x : C` turns a morphism `v ‚ü∂ w` into a morphism `v ‚äó x ‚ü∂ w ‚äó x`. -/
def whiskerRight {x : C} {v w : V} (vx : Tensor v x) (wx : Tensor w x)
  : Ehom V V v w ‚ü∂ Ehom V C vx.obj wx.obj :=
  (ihom v).map wx.cone ‚â´ vx.coneNatTransInv _

/-- Since right whiskering is defined by postcomposition with `Tensor.coneNatTransInv`,
  its postcomposition with `Pretensor.coneNatTrans` can be simplified. -/
lemma whiskerRight_coneNatTrans
  {v w : V} {x : C} (vx : Tensor v x) (wx : Tensor w x)
  : whiskerRight V vx wx ‚â´ vx.coneNatTrans _ = (ihom v).map wx.cone
  := by
  unfold whiskerRight
  rw [Category.assoc]
  rw [vx.NatTransInv_NatTrans_eq]
  rw [Category.comp_id]

/-- Whiskering the identity on the right yields the identity again. -/
lemma whiskerRight_id
  {v : V} {x : C} (vx : Tensor v x)
  : eId V v ‚â´ whiskerRight V vx vx = eId V vx.obj
  := by
  -- Move vx.coneNatTrans to the RHS
  unfold whiskerRight
  rw [‚Üê Category.assoc]
  rw [‚Üê IsIso.eq_comp_inv (vx.coneNatTransInv vx.obj)]
  rw [IsIso.inv_eq_of_hom_inv_id (vx.NatTransInv_NatTrans_eq _)]
  -- Turn eId with eComp into œÅ
  unfold Pretensor.coneNatTrans
  rw [‚Üê curry_natural_left]
  rw [whisker_exchange_assoc]
  rw [‚Üê Iso.hom_inv_id_assoc (œÅ_ _) (_ ‚â´ eComp _ _ _ _)]
  rw [e_comp_id]
  rw [Category.comp_id]
  -- Move vx.cone to the right of curry, and turn curry œÅ into eId
  rw [rightUnitor_naturality (vx.cone)]
  rw [curry_natural_right]
  rw [‚Üê MonoidalClosed.id_eq]
  rfl

/-- An auxiliary lemma for what is to come. -/
lemma cone_comp_whiskerRight_eq {x : C} {w v : V} (wx : Tensor w x) (vx : Tensor v x) :
  (wx.cone ‚äó‚Çò whiskerRight V wx vx) ‚â´ eComp V _ _ _
  = (ihom.ev w).app v ‚â´ vx.cone
  := by
  -- Remove an instance of w ‚óÅ (ihom w).map vx from both sides
  rw [tensorHom_def'_assoc]
  rw [‚Üê ihom.ev_naturality]
  unfold whiskerRight
  rw [whiskerLeft_comp_assoc]
  apply whisker_eq
  -- Move w ‚óÅ wx.coneNatTrans to the RHS
  rw [‚Üê IsIso.eq_inv_comp]
  rw [inv_whiskerLeft]
  rw [IsIso.inv_eq_of_hom_inv_id (wx.NatTransInv_NatTrans_eq _)]
  -- The resulting RHS is uncurry (curry (LHS))
  rw [‚Üê uncurry_eq]
  rw [wx.uncurry_coneNatTrans]

/-- Whiskering on the right commutes with morphism composition. -/
lemma whiskerRight_comp {u v w : V} {x : C} (ux : Tensor u x) (vx : Tensor v x) (wx : Tensor w x)
  : eComp V u v w ‚â´ whiskerRight V ux wx
    = (whiskerRight V ux vx ‚äó‚Çò whiskerRight V vx wx) ‚â´ eComp V ux.obj vx.obj wx.obj
  := by
  -- Move coneNatTrans to the RHS
  show (_ ‚â´ _ ‚â´ _ = _)
  rw [‚Üê Category.assoc]
  rw [‚Üê IsIso.eq_comp_inv (ux.coneNatTransInv wx.obj)]
  rw [IsIso.inv_eq_of_hom_inv_id (ux.NatTransInv_NatTrans_eq _)]
  rw [Category.assoc]
  -- Move the curry from coneNatTrans to around eComp
  unfold Pretensor.coneNatTrans
  repeat rw [‚Üê curry_natural_left]
  rw [eq_curry_iff]
  rw [uncurry_natural_right]
  -- eComp contains a curry, which we can cancel with the uncurry
  show (uncurry (curry ((Œ±_ _ (Ehom _ _ _ _) (Ehom _ _ _ _)).inv ‚â´ _)) ‚â´ _ = _)
  rw [uncurry_curry]
  repeat rw [Category.assoc]
  -- Split the tensored morphism into two whiskers, and move them
  rw [tensorHom_def']
  rw [whiskerLeft_comp_assoc]
  rw [whisker_exchange_assoc]
  -- Move the associator to the RHS and use associativity on _ ‚óÅ eComp V ux vx wx ‚â´ eComp V x ux wx
  rw [Iso.inv_comp_eq]
  rw [‚Üê associator_naturality_right_assoc]
  rw [‚Üê associator_naturality_middle_assoc]
  rw [‚Üê associator_naturality_left_assoc]
  rw [e_assoc']
  -- Use cone_comp_whiskerRight_eq a first time
  rw [‚Üê comp_whiskerRight_assoc (ux.cone ‚ñ∑ _)]
  rw [‚Üê comp_whiskerRight_assoc]
  rw [‚Üê tensorHom_def'_assoc (ux.cone)]
  rw [cone_comp_whiskerRight_eq]
  rw [comp_whiskerRight_assoc]
  -- Use cone_comp_whiskerRight_eq a second time
  rw [whisker_exchange_assoc]
  rw [‚Üê tensorHom_def'_assoc]
  rw [cone_comp_whiskerRight_eq]
  rfl

-- We only need symmetry on V for whiskerLeft and everything that follows from it
variable [SymmetricCategory V]

/-- Whiskering on the left by `v : V` turns a morphism `x ‚ü∂ y` into a morphism `v ‚äó x ‚ü∂ v ‚äó y`. -/
def whiskerLeft {x y : C} {v : V} (vx : Tensor v x) (vy : Tensor v y)
  : Ehom V C x y ‚ü∂ Ehom V C vx.obj vy.obj :=
  curry ((Œ≤_ _ _).hom ‚â´ _ ‚óÅ vy.cone ‚â´ eComp _ _ _ _) ‚â´ vx.coneNatTransInv _

/-- Since left whiskering is defined by postcomposition with `Tensor.coneNatTransInv`,
  its postcomposition with `Pretensor.coneNatTrans` can be simplified. -/
lemma whiskerLeft_coneNatTrans_eq {x y : C} {v : V} (vx : Tensor v x) (vy : Tensor v y)
  : whiskerLeft V vx vy ‚â´ vx.coneNatTrans _ = curry ((Œ≤_ _ _).hom ‚â´ _ ‚óÅ vy.cone ‚â´ eComp V _ _ _)
  := by
  unfold whiskerLeft
  rw [Category.assoc]
  rw [vx.NatTransInv_NatTrans_eq]
  apply Category.comp_id

/-- Whiskering the identity on the left yields the identity again. -/
lemma whiskerLeft_id {v : V} {x : C} (vx : Tensor v x)
  : eId V x ‚â´ whiskerLeft V vx vx = eId V (vx.obj)
  := by
  unfold whiskerLeft
  rw [‚Üê Category.assoc]
  rw [‚Üê IsIso.eq_comp_inv (vx.coneNatTransInv vx.obj)]
  rw [IsIso.inv_eq_of_hom_inv_id (vx.NatTransInv_NatTrans_eq _)]
  unfold Pretensor.coneNatTrans
  repeat rw [‚Üê curry_natural_left]
  congrm curry ?_
  rw [braiding_naturality_right_assoc]
  rw [braiding_tensorUnit_right]
  rw [Category.assoc]
  rw [‚Üê Iso.eq_inv_comp _]
  rw [‚Üê whisker_exchange_assoc (eId _ _), whisker_exchange_assoc _ (eId _ _)]
  rw [‚Üê leftUnitor_inv_naturality_assoc, ‚Üê rightUnitor_inv_naturality_assoc]
  rw [e_id_comp, e_comp_id]

/-- An auxiliary lemma for what is to come. -/
lemma cone_comp_whiskerLeft_eq {x y : C} {v : V} (vx : Tensor v x) (vy : Tensor v y)
  : (Œ≤_ _ _).hom ‚â´ (vx.cone ‚äó‚Çò whiskerLeft V vx vy) ‚â´ eComp V x vx.obj vy.obj
    = Ehom V C x y ‚óÅ vy.cone ‚â´ eComp V x y vy.obj
  := by
  rw [‚Üê Iso.eq_inv_comp]
  rw [‚Üê SymmetricCategory.braiding_swap_eq_inv_braiding]
  rw [tensorHom_def'_assoc]
  rw [‚Üê Pretensor.uncurry_coneNatTrans]
  rw [‚Üê uncurry_natural_left]
  rw [whiskerLeft_coneNatTrans_eq]
  apply uncurry_curry

/-- Whiskering on the left commutes with morphism composition. -/
lemma whiskerLeft_comp {v : V} {x y z : C} (vx : Tensor v x) (vy : Tensor v y) (vz : Tensor v z)
  : eComp V x y z ‚â´ whiskerLeft V vx vz
    = (whiskerLeft V vx vy ‚äó‚Çò whiskerLeft V vy vz) ‚â´ eComp V vx.obj vy.obj vz.obj
  := by
  -- Move vx.coneNatTrans to the RHS
  show (_ ‚â´ _ ‚â´ _ = _)
  rw [‚Üê Category.assoc]
  rw [‚Üê IsIso.eq_comp_inv (vx.coneNatTransInv vz.obj)]
  rw [IsIso.inv_eq_of_hom_inv_id (vx.NatTransInv_NatTrans_eq _)]
  -- Work the LHS
  rw [‚Üê curry_natural_left]
  rw [braiding_naturality_right_assoc]
  rw [‚Üê whisker_exchange_assoc]
  -- Work the RHS
  unfold Pretensor.coneNatTrans
  rw [‚Üê curry_natural_left]
  congrm curry ?_
  -- Turn Œ≤_ into a bunch of Œ±_ and Œ≤_ morphisms on the RHS
  rw [‚Üê Iso.eq_inv_comp]
  rw [‚Üê SymmetricCategory.braiding_swap_eq_inv_braiding]
  rw [braiding_tensor_left_hom_assoc]
  -- Split whiskerLeft _ vx vy ‚äó‚Çò whiskerLeft _ vy vz
  rw [tensorHom_def_assoc]
  rw [MonoidalCategory.whiskerLeft_comp_assoc]
  rw [MonoidalCategory.whiskerLeft_comp_assoc]
  -- Get rid of one Œ±_, together with a v ‚óÅ eComp V vx vy vz
  rw [whisker_exchange_assoc _ (eComp V vx.obj vy.obj vz.obj)]
  rw [‚Üê associator_naturality_middle_assoc]
  rw [‚Üê associator_naturality_right_assoc]
  rw [‚Üê associator_naturality_left_assoc]
  rw [e_assoc']
  -- Get rid of one Œ≤_, together with a v ‚óÅ whiskerLeft V vy vz
  rw [whisker_exchange_assoc _ (whiskerLeft _ _ _)]
  rw [whisker_exchange_assoc _ (whiskerLeft _ _ _)]
  rw [‚Üê comp_whiskerRight_assoc (vx.cone ‚ñ∑ _)]
  rw [‚Üê comp_whiskerRight_assoc (v ‚óÅ whiskerLeft _ _ _)]
  rw [‚Üê comp_whiskerRight_assoc (Œ≤_ _ _).hom]
  rw [‚Üê tensorHom_def'_assoc vx.cone (whiskerLeft _ _ _)]
  rw [cone_comp_whiskerLeft_eq]
  rw [comp_whiskerRight_assoc]
  -- Get rid of one Œ±_, together with a eComp V x y vy.obj ‚ñ∑ Ehom V C y z
  rw [‚Üê whisker_exchange_assoc (eComp V x y vy.obj)]
  rw [‚Üê associator_inv_naturality_middle_assoc]
  rw [‚Üê associator_inv_naturality_right_assoc]
  rw [e_assoc]
  -- Use cone_comp_whiskerLeft_eq
  rw [‚Üê whiskerLeft_comp_assoc _ (_ ‚óÅ whiskerLeft _ _ _)]
  rw [‚Üê whiskerLeft_comp_assoc _ (vy.cone ‚ñ∑ _)]
  rw [‚Üê whiskerLeft_comp_assoc _ (Œ≤_ _ _).hom]
  rw [‚Üê tensorHom_def_assoc vy.cone (whiskerLeft _ _ _)]
  rw [cone_comp_whiskerLeft_eq]
  rw [MonoidalCategory.whiskerLeft_comp_assoc]
  -- Get rid of the last Œ±_
  rw [‚Üê associator_naturality_right_assoc]
  rw [e_assoc']

/-- Right whiskering commutes with left whiskering. -/
lemma whiskerRight_right_eq_whiskerLeft_left {v w : V} {x y : C} (vx : Tensor v x) (wx : Tensor w x) (vy : Tensor v y) (wy : Tensor w y)
  : (whiskerRight V vx wx ‚äó‚Çò whiskerLeft V wx wy) ‚â´ eComp V vx.obj wx.obj wy.obj
    = (whiskerRight V vy wy ‚äó‚Çò whiskerLeft V vx vy) ‚â´
      (Œ≤_ _ _).hom ‚â´ eComp V vx.obj vy.obj wy.obj
  := by
  -- Move Œ≤ to the left of the RHS
  rw [braiding_naturality_assoc]
  -- Split the tensored morphisms into double whiskers
  rw [tensorHom_def'_assoc]
  rw [tensorHom_def'_assoc]
  -- Add coneNatTrans to the end, get everything inside its curry and remove the curry
  rw [‚Üê cancel_mono (vx.coneNatTrans _)]
  repeat rw [Category.assoc _ _ (vx.coneNatTrans wy.obj)]
  unfold Pretensor.coneNatTrans
  repeat rw [‚Üê curry_natural_left]
  apply congr_arg curry
  -- On both sides: Reassociate _ ‚óÅ eComp ‚â´ eComp, which gives an Œ±, allows us to reassociate a couple of whiskerings, and ultimately allows us to apply the whisker..._selfEval_comp_eq lemmas
  repeat rw [whisker_exchange_assoc (vx.cone) (eComp _ _ _ _)]
  repeat rw [‚Üê e_assoc]
  repeat rw [associator_inv_naturality_left_assoc]
  repeat rw [associator_inv_naturality_middle_assoc]
  repeat rw [‚Üê comp_whiskerRight_assoc (vx.cone ‚ñ∑ _)]
  rw [‚Üê comp_whiskerRight_assoc (v ‚óÅ whiskerRight _ _ _), ‚Üê comp_whiskerRight_assoc (v ‚óÅ whiskerLeft _ _ _)]
  repeat rw [‚Üê tensorHom_def'_assoc]
  rw [‚Üê Iso.inv_hom_id_assoc (Œ≤_ _ _) ((_ ‚äó‚Çò whiskerLeft _ _ _) ‚â´ _)]
  rw [cone_comp_whiskerRight_eq, cone_comp_whiskerLeft_eq]
  repeat rw [comp_whiskerRight_assoc]
  -- Reassociates the RHS again, which gives another Œ±, and then on both sides: reassociate and reorder a couple of whiskerings to apply the whisker..._selfEval_comp_eq lemmas again
  rw [‚Üê e_assoc']
  repeat rw [associator_inv_naturality_right_assoc]
  rw [whisker_exchange_assoc _ (whiskerLeft _ _ _), whisker_exchange_assoc _ (whiskerRight _ _ _)]
  rw [associator_naturality_middle_assoc]
  rw [associator_naturality_right_assoc]
  rw [‚Üê whiskerLeft_comp_assoc _ (vy.cone ‚ñ∑ _)]
  rw [‚Üê whiskerLeft_comp_assoc _ (_ ‚óÅ whiskerRight _ _ _)]
  repeat rw [‚Üê tensorHom_def'_assoc]
  dsimp [Functor.id_obj]
  rw [‚Üê Iso.inv_hom_id_assoc (Œ≤_ _ _) ((_ ‚äó‚Çò _) ‚â´ _)]
  rw [cone_comp_whiskerLeft_eq, cone_comp_whiskerRight_eq]
  repeat rw [whiskerLeft_comp_assoc]
  -- Combine an instance of Œ± from the LHS and two instances of Œ± and two whiskered instances of Œ≤ from the RHS into Œ≤ (v ‚äó Ehom) Ehom
  rw [Iso.inv_comp_eq]
  rw [‚Üê braiding_swap_eq_inv_braiding (Ehom V C x y) v]
  rw [‚Üê braiding_tensor_left_hom_assoc]
  -- Move Œ≤ to the right place, which concludes the proof
  rw [braiding_inv_naturality_left_assoc]
  rw [‚Üê braiding_swap_eq_inv_braiding]

class TensoredCategory (V : Type u) [Category.{u‚ÇÅ} V] [MonoidalCategory V] [MonoidalClosed V]
    [SymmetricCategory V] (C : Type v) [EnrichedCategory V C] where
  tensor : (v : V) ‚Üí (x : C) ‚Üí Tensor v x

open TensoredCategory

/-- Tensoring, together with whiskering, constitutes a V-enriched functor `tensor_bifunc : V ‚äó C ‚ü∂ C` --/
def tensor_bifunc [TensoredCategory V C] : EnrichedFunctor V (V ‚äó[V] C) C :=
  enrichedTensor.eBifuncConstr V V C
    (fun v x ‚Ü¶ (tensor v x).obj)
    (fun v w x ‚Ü¶ whiskerRight V (tensor v x) (tensor w x))
    (fun v x y ‚Ü¶ whiskerLeft V (tensor v x) (tensor v y))
    (fun v x ‚Ü¶ whiskerRight_id V (tensor v x))
    (fun v x ‚Ü¶ whiskerLeft_id V (tensor v x))
    (fun u v w x ‚Ü¶ whiskerRight_comp V (tensor u x) (tensor v x) (tensor w x))
    (fun v x y z ‚Ü¶ whiskerLeft_comp V (tensor v x) (tensor v y) (tensor v z))
    (fun v w x y ‚Ü¶ whiskerRight_right_eq_whiskerLeft_left V (tensor v x) (tensor w x) (tensor v y) (tensor w y))

end Tensor

end Enriched

end CategoryTheory
